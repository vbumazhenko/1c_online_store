
#Область ПрограммныйИнтерфейс

Функция ПолучитьСоединение(ПараметрыУзла) Экспорт
	
	СтруктураURI = ОбщегоНазначенияКлиентСервер.СтруктураURI(ПараметрыУзла.СтрокаПодключения);
	Схема = ?(ЗначениеЗаполнено(СтруктураURI.Схема), СтруктураURI.Схема, "http");
	Прокси = ПолучениеФайловИзИнтернета.ПолучитьПрокси(Схема);
	
	Попытка
		
		HTTPСоединение = Новый HTTPСоединение(
			СтруктураURI.Хост,
			СтруктураURI.Порт,,,
			Прокси,
			1200,
			?(НРег(Схема) = "http", Неопределено, ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение()));
		Возврат HTTPСоединение;
		
	Исключение
		
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Обмен с инетрнет-магазином'", ОбщегоНазначения.КодОсновногоЯзыка()),
			УровеньЖурналаРегистрации.Предупреждение,
			ПараметрыУзла.УзелОбмена.Метаданные(),
			ПараметрыУзла.УзелОбмена,
			ОписаниеОшибки + " " + НСтр("ru = 'Обмен отменен.'"));
		
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти
